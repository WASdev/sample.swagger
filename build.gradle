apply plugin: 'war'
apply plugin: 'liberty'

group = 'net.wasdev.wlp.swagger-sample'
version = '1.0-SNAPSHOT'

description = """WAS Liberty Sample - Using Swagger in Liberty"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.0-SNAPSHOT'
    }
}

repositories {
     mavenCentral()
}

ext {
    // Liberty server properties
    wlpServerName = 'swaggerSample'
    serverDirectory = "${project.buildDir}/wlp/usr/servers/${wlpServerName}"
    testServerHttpPort = 9081
    testServerHttpsPort = 9444

    // This is set in the ibm-web-ext.xml file
    warContext = 'swagger-sample'

}

war {
	archiveName = baseName + '.' + extension
}

liberty {
    server{
        configFile = file("src/main/liberty/config/server.xml")
        name = 'LibertyProjectServer'
        apps = [war]
     }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version:'3.1.1'
    providedCompile group: 'javax.json', name: 'javax.json-api', version:'1.0'
    providedCompile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version:'2.0'
    providedCompile group: 'io.swagger', name: 'swagger-annotations', version:'1.5.3'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
	libertyRuntime group: 'com.ibm.websphere.appserver.runtime', name: 'wlp-webProfile7', version: '17.0.0.2'
}

test {
    println 'inside the test block'
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
    exclude '**/it/**'
}

task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-results/it")
    include '**/it/**'
    exclude '**/unit/**'

    systemProperties = ['liberty.test.port': testServerHttpPort, 'war.context': warContext]
}

task printMessageAboutRunningServer {
    doLast {
        println "The server is now running at http://localhost:${testServerHttpPort}/${warContext}"
        println "To stop the server run 'gradle libertyStop'"
    }
}

check.dependsOn 'integrationTest'
integrationTest.dependsOn 'libertyStart', 'testClasses'
integrationTest.finalizedBy 'libertyStop'
libertyStart.finalizedBy 'printMessageAboutRunningServer'
